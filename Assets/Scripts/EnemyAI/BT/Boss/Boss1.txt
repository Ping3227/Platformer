tree("Root")
	sequence
		CalculateDistance
		// If far => attack, If too close => Dodge 
		fallback
			sequence 
				CloserThan(3.0)
				tree("Dodge")
			sequence 
				CloserThan(8.0)
				tree("Normal Attack")
			sequence
				tree("Special Attack")

					
			
// This region is attack action		
tree("Normal Attack")
	sequence
		FindNextLocation(false)
		Teleport(0.7)
		Attack
		Wait(1.0)

tree("Special Attack")
		random(0.5,0.5)
			tree("Consecutive Attack")
			tree("Immobolize")
		

tree("Consecutive Attack")
	sequence
		mute 
			repeat
				sequence 
					IsPlayerInfront
					ApproachPlayer(3.0)
					Teleport(4.0)
					CalculateDistance
					mute 
						sequence 
							CloserThan(2.0)
							Attack
					not CloserThan(3.0)
		IsPlayerInfront
		GoBehindPlayer
		Teleport(4.0)
		Attack
		
tree("Immobolize")
	sequence 
		Immobolize
		Wait(1.0)
		IsImmobilized
		GoBehindPlayer
		Teleport(6.0)
		Attack
		ResetImmobilized

// This region is for defense action		
tree("Dodge")
	sequence
		FindNextLocation(true)
		Teleport(0.7)
		Wait(1)
		CalculateDistance
		CloserThan(4.0)
		tree("Counter Attack")

tree("Counter Attack")
	random(0.3,0.7)
		Blocking
		tree("Strong Attack")

tree("Strong Attack")
	sequence
		Wait(0.5)
		StrongAttack
		Wait(1.5)