tree("Root")
	sequence
		CalculateDistance
		// If far => attack, If too close => Dodge 
		fallback
			sequence 
				CloserThan(3.0)
				tree("Dodge")
			sequence 
				CloserThan(7.0)
				Wait(0.5)
				tree("Normal Attack")
			sequence
				Wait(0.5)
				tree("Special Attack")

					
			
// This region is attack action		
tree("Normal Attack")
	sequence
		GoToPlayer
		Attack(1.0,false,true)
		Wait(1.0)

tree("Special Attack")
		random(0.3,0.3)
			tree("Consecutive Attack")
			tree("Ray Attack")
		


tree("Consecutive Attack")
	sequence
		// Initialial setup
		GoToCorner
		Teleport(2.0,true)
		Wait(2.0)		
		CalculateDistance
		// first teleport
		fallback
			sequence
				// attack
				CloserThan(5.0)
				IsPlayerLowerthan(1.0)
				GoBeforePlayer
				Attack(2.0,true,false)
				Wait(1.5)
			sequence
				// not attack
				GoForward(3.0)
				Teleport(5.0,false)
				Wait(0.3)
				sequence
				// second teleport
					CalculateDistance
					IsPlayerInfront
					fallback 
						sequence
						    // Attack
							not CloserThan(2.0)
							CloserThan(5.0)
							IsPlayerLowerthan(1.0)
							GoBeforePlayer
							Attack(3.0,true,false)
							Wait(1.5)
						sequence
							// not attack
							GoForward(3.0)
							Teleport(10.0,false)
							Wait(0.3)
							CalculateDistance
							fallback
								sequence
								   // Attack
									not CloserThan(2.0)
									CloserThan(5.0)
									IsPlayerLowerthan(1.0)
									IsPlayerInfront
									GoBeforePlayer
									Attack(4.0,true,false)
									Wait(2.0)
								sequence
									//attack far away
									GoForward(5.0)
									Attack(4.0,true,false)
									Wait(2.0)
		
		

// This region is for defense action		
tree("Dodge")
	sequence
		DodgePlayer
		Teleport(0.7,true)
		Wait(1)
		CalculateDistance
		CloserThan(4.0)
		random(0.5,0.5)
			tree("Strong Attack")
			DodgePlayer


tree("Strong Attack")
	fallback
		sequence
			CloserThan(1.0)
			Wait(1)
			StrongAttack
			Wait(1.5)
		sequence
			Wait(0.5)
			StrongAttack
			Wait(1.5)
			

tree("Ray Attack")
	sequence
		RayAim(1.0,true)
		RayAttack(2.0,false,false)
	
		