tree("Root") 
	random(0.7,0.3)
		tree("Normal Attack")
		tree("Special Attack")
		
tree("Normal Attack")
	sequence
		mute 
			sequence 
				CalculateDistance
				CloserThan(2.0)
				DodgePlayer
				Teleport(2.0,false)
				Wait(1.0)
		GoToPlayer
		Attack(1.0,false,true)
		Wait(2.0)
		CalculateDistance
		CloserThan(3.0)
		Random(0.5,0.5)
			tree("Dodge")
			tree("Strong Attack")


tree("Special Attack")
	sequence
		Immobolize
		random(0.3,0.3)
			tree("Consecutive Attack")
			tree("Ray Attack")
		CloserThan(3.0)
		tree("Dodge")
		

tree("Consecutive Attack")
	sequence
		// Initialial setup
		GoToCorner
		Teleport(2.0,true)
		Wait(2.0)		
		CalculateDistance
		// first teleport
		fallback
			sequence
				// attack
				CloserThan(5.0)
				IsPlayerLowerthan(1.0)
				GoBeforePlayer
				Attack(2.0,true,false)
				Wait(2.0)
			sequence
				// not attack
				GoForward(3.0)
				Teleport(5.0,false)
				Wait(0.3)
				sequence
				// second teleport
					CalculateDistance
					IsPlayerInfront
					fallback 
						sequence
						    // Attack
							not CloserThan(2.0)
							CloserThan(5.0)
							IsPlayerLowerthan(1.0)
							GoBeforePlayer
							Attack(3.0,true,false)
							Wait(2.0)
						sequence
							// not attack
							GoForward(3.0)
							Teleport(10.0,false)
							Wait(0.3)
							CalculateDistance
							fallback
								sequence
								   // Attack
									not CloserThan(2.0)
									CloserThan(5.0)
									IsPlayerLowerthan(1.0)
									IsPlayerInfront
									GoBeforePlayer
									Attack(4.0,true,false)
									Wait(2.0)
								sequence
									//attack far away
									GoForward(5.0)
									Attack(4.0,true,false)
									Wait(2.5)

// This region is for defense action		
tree("Dodge")
	sequence
		Immobolize
		DodgePlayer
		Teleport(2.0,true)
		Wait(1.5)
		

tree("Counter Attack")
	random(0.3,0.7)
		Blocking
		tree("Strong Attack")

tree("Strong Attack")
	sequence 
		StrongAttack
		Wait(1.5)
		CalculateDistance
		CloserThan(3.0)
		tree("Dodge")	

tree("Ray Attack")
	sequence
		mute 
			sequence 
				CalculateDistance
				CloserThan(3.0)
				DodgePlayer
				Teleport(1.0,true)
		RayAim(1.3)
		RayAttack(1.)
		Wait(1.0)
	
		